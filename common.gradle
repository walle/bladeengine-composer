buildscript {
  repositories { jcenter() }
}

ext {
  appName = 'bladeengine-composer'
  nameAppendix = project != rootProject ? project.name : ''
  nameFull = appName + (nameAppendix.empty ? '' : "-$nameAppendix")
  libgdxVersion = '1.2.1-SNAPSHOT'
}

group = 'org.bladecoder.bladeengine.composer'
version = '1.0.0'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'announce'

repositories {
  mavenCentral()
  mavenLocal()
  maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

// java

sourceCompatibility = 1.7

sourceSets {
  main {
    java.srcDirs = ['src/main/java']
    resources.srcDirs = ['src/main/java']
  }
  test.java.srcDirs = ['src/test/java']
  site.resources.srcDirs = [javadoc.destinationDir.parentFile, testReportDir.parentFile]
}

jar {
  baseName = appName
  appendix = nameAppendix
  manifest.attributes += [
    'web': 'http://bladecoder.org',
    'github': 'https://github.com/bladecoder/bladeengine-composer',
    'bintray': "http://bintray.com/bladecoder/generic/bladeengine-composer${nameAppendix.empty ? '' : "-$nameAppendix"}",
    'license': 'Apache-2.0',
    'group': project.group,
    'artifact': nameFull,
    'version': project.version,
    'libgdx': libgdxVersion,
    'java': targetCompatibility,
    'timestamp': System.currentTimeMillis()
  ]
}

javadoc {
  title = nameFull
  options {
    memberLevel = JavadocMemberLevel.PRIVATE
    author true
    setUse true
    encoding "UTF-8"
  }
}

// IDE integration

eclipse {
  project.name = nameFull
  classpath {
    sourceSets -= sourceSets.site
    containers = ["org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-$targetCompatibility"]
  }
}

// methods
Object property(String property) {
  return hasProperty(property) ? project[property] : ''
}